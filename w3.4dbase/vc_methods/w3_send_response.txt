//%attributes = {"lang":"en","invisible":true,"shared":true,"publishedWeb":true} comment added and reserved by 4D.
//Handles HTTP responses. Can send HTML, JSON and Redirects

//$1 TEXT-Response(json, html)-Ignored if$2 is 301
//{$2}LONGINT-HTTP status code(default: 200)-Use 301 to redirect
//{$3}TEXT-Content-Type(default: text/html)(It is good practice to send response with the proper content-Type)
//{$4}TEXT-Redirect URL(This is ignored if status code is NOT 301)

C_TEXT($0)
C_TEXT($1;$response)
C_LONGINT($2;$status_code)
C_TEXT($3;$content_type)
C_TEXT($4;$url_redirect)
$cp:=Count parameters

If ($cp>=1)
$response:=$1
End if 

If ($cp>=2)
$status_code:=$2
Else 
$status_code:=200// Ok
End if 

If ($cp>=3)
$content_type:=$3
Else 
$content_type:="text/html"
End if 

If ($cp>=4)
$url_redirect:=$4
End if 

ARRAY TEXT($fieldArray;0)
ARRAY TEXT($valueArray;0)

APPEND TO ARRAY($fieldArray;"X-VERSION")
APPEND TO ARRAY($fieldArray;"X-STATUS")

APPEND TO ARRAY($valueArray;"HTTP/1.1")
APPEND TO ARRAY($valueArray;String($status_code))

WEB SET HTTP HEADER($fieldArray;$valueArray)

Case of 
: ($status_code=301)
WEB SEND HTTP REDIRECT($url_redirect)
Else 
WEB SEND TEXT($response;$content_type)
End case 